use std::rc::Rc;
use std::str::FromStr;
use std::collections::VecDeque;
use parse_tree::*;
use types::*;
use operators::*;

grammar;

Int: i32 = {
    r"[0-9]+" => i32::from_str(<>).unwrap()
}

Bool: bool = {
    "true" => true,
    "false" => false
}

Str: String = {
    r#""[^"]*""# => {
      let a = <>;
      a[1..a.len()-1].to_string()
    }
};

Unit: () = {
    "()" => ()
}

Identifier: Identifier = {
    r"[a-zA-Z_.][.a-zA-Z_0-9']*(\.:[.+/*=-]+)?" => (<>).to_string()
}

Pat: Rc<Pat> = {
    Identifier => Rc::new(Pat::Var(<>))
}

Type: Rc<Type> = {
    <t_in:TypePrimitive> "->" <t_out:Type> => Rc::new(Type::Arrow(<>)),
    TypePrimitive
}

TypePrimitive: Rc<Type> = {
    "()" => Rc::new(Type::Unit),
    "int" => Rc::new(Type::Int),
    "bool" => Rc::new(Type::Bool),
    "string" => Rc::new(Type::String)
}

Rule: Rule = {
    "|" <pat:Pat> "->" <e:Expr> => (pat, e)
}

TypeAnn: Rc<Type> = {
    ":" <t:Type> => t
}

FuncArg: FuncArg = {
    <id:Identifier> <typ:TypeAnn?> => (id, typ)
}

CommaFuncArg: FuncArg = {
    "," <id:Identifier> <typ:TypeAnn?> => (id, typ)
}

TypeAnnFuncOut: Rc<Type> = {
    "->" <t:Type> => t
}

Block: Rc<Expr> = {
    "{" <statements:Stmt*> "}" => Rc::new(Expr::Block(VecDeque::from(statements)))
}

pub Stmt: Rc<Stmt> = {
    SemicolonOp => Rc::new(Stmt::EmptyHole),
    <e:Expr> SemicolonOp => Rc::new(Stmt::Expr(e)),
    "let" <pat:Pat> "=" <e:Expr> SemicolonOp => Rc::new(Stmt::Let(pat, None, e)),
    "let" <pat:Pat> ":" <typ:Type> "=" <e:Expr> SemicolonOp => Rc::new(Stmt::Let(pat, Some(typ), e)),
}

pub Expr: Rc<Expr> = {
    "func" "(" <func_arg:FuncArg> <func_args:CommaFuncArg*> ")" <t_out:TypeAnnFuncOut?> "{" <body:Expr> "}" => Rc::new(Expr::Func(func_arg, func_args, t_out, body)),
    "if" <e1:Expr> "{" <e2:Expr> "} else {" <e3:Expr> "}" => Rc::new(Expr::If(<>)),
    "match" <e1:Expr> "{" <rules:Rule*> "}" => Rc::new(Expr::Match(e1, rules)),
    <block:Block> => block,
    EqualityOpExpr,
}

CommaExpr: Rc<Expr> = {
    "," <expr:Expr> => expr
}

EqualityOpExpr: Rc<Expr> = {
    EqualityOpExpr EqualityOp ComparisonOpExpr => Rc::new(Expr::BinOp(<>)),
    ComparisonOpExpr
}

ComparisonOpExpr: Rc<Expr> = {
    ComparisonOpExpr ComparisonOp SummandOpExpr => Rc::new(Expr::BinOp(<>)),
    SummandOpExpr
}

SummandOpExpr: Rc<Expr> = {
    SummandOpExpr SummandOp FactorOpExpr => Rc::new(Expr::BinOp(<>)),
    FactorOpExpr
}

FactorOpExpr: Rc<Expr> = {
    FactorOpExpr FactorOp Term => Rc::new(Expr::BinOp(<>)),
    FuncApExpr
}

FuncApExpr: Rc<Expr> = {
    <f:FuncApExpr> "(" <e:Expr> <es:CommaExpr*> ")" => Rc::new(Expr::FuncAp(f, e, VecDeque::from(es))),
    Term
}

Term: Rc<Expr> = {
    Identifier => Rc::new(Expr::Var(<>)),
    Unit => Rc::new(Expr::Unit),
    Int => Rc::new(Expr::Int(<>)),
    Bool => Rc::new(Expr::Bool(<>)),
    Str => Rc::new(Expr::Str(<>)),
    "(" <Expr> ")",
}

FactorOp: BinOpcode = {
    "*" => BinOpcode::Multiply,
    "/" => BinOpcode::Divide
}

SummandOp: BinOpcode = {
    "+" => BinOpcode::Add,
    "-" => BinOpcode::Subtract,
}

EqualityOp: BinOpcode = "==" => BinOpcode::Equals;

ComparisonOp: BinOpcode = {
    "<" => BinOpcode::LessThan,
    "<=" => BinOpcode::LessThanOrEquals,
    ">" => BinOpcode::GreaterThan,
    ">=" => BinOpcode::GreaterThanOrEquals,
}

SemicolonOp: BinOpcode = ";" => BinOpcode::Semicolon;